plugins {
    id 'fabric-loom' version '0.12.10'
    id 'maven-publish'
    id 'com.palantir.git-version' version '0.15.0'
}

group = project.maven_group

version '0.0.0-SNAPSHOT'

try {
    def details = versionDetails()
    version = details.lastTag.substring(1)
    if (details.commitDistance > 0) {
        version += "+" + details.commitDistance + "." + details.gitHash;
    }
    def ENV = System.getenv()
    if (!ENV.GITHUB_ACTIONS && !ENV.JITPACK) {
        version += details.isCleanTag ? "+local" : "+dirty"
    }
} catch (ex) {
    println ex
}

repositories {
    mavenCentral()
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://jitpack.io" }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "mod_id": mod_id, "version": project.version
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "com.gitlab.Lortseam:completeconfig:1.4.0"
    include modImplementation("moe.hertz:side-effects:0.1.3")

    compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.mod_id
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
